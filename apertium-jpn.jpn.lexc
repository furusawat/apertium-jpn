! Morphological Transducer for Japanese

Multichar_Symbols

! Part of speech categories
%<n%>   ! Noun
%<pn%>  ! Proper noun
%<v%>	! Verb
%<adj%> ! Adjective
%<adv%> ! Adverb
%<cnjcoo%> ! Coordinating conjunction
%<ij%>  ! Interjection
%<prn%> ! Pronoun
%<cop%>	! Copula
%<num%> ! Numeral
%<det%> ! Determiner

%<dem%> ! Demonstrative

%<top%> ! toponym (should probably be changed?)

%<attr%> ! for na adjectives

! Particles
%<tpc%> ! Topic
%<nom%>  ! Nominative
%<gen%>  ! Genitive
%<acc%>  ! Accusative
%<dat%>  ! Dative
%<quot%> ! Quotative
%<all%> ! Allative
%<abl%>  ! Ablative
%<ins%>  ! Instrumental
%<com%>  ! Comitative
%<term%> ! Terminative
%<mod%>  ! For final particles
%<abs%>  ! For things that don't have postpositions

! -alisers
%<nz%>  ! Nominaliser
%<az%>  ! Adverbaliser
%<vz%>  ! Verbaliser

! Pronoun-related
%<p1%>   ! First person
%<p2%>   ! Second person
%<p3%>   ! Third person
%<pers%> ! Personal

! verb suffixes
%<vol%>  ! Volitional
%<neg%>  ! Negative
%<past%> ! Past
%<imp%>  ! Imperative
%<npst%> ! Non-past
!conditional?
%<caus%>  ! Causative
%<pot%>   ! Potential
%<pass%>  ! Passive
%<cvb_te%> ! te form, because I don't know what it is
%<prog%> ! progressive

! Number morphology
%<pl%>  ! Plural
%<sg%>  ! Singular

! Formality
%<pol%>	! Polite
%<ifml%>! Informal

!Punctuation
%<sent%>
%<cm%>
%<lquot%>
%<rquot%>
%<lpar%>
%<rpar%>

! Other symbols
%>      ! Morpheme boundary
%{っ%} ! for use in ta and te form
%{a%} ! for use in neg form
%{た%} ! for use in ta form
%{て%} ! for te form
%{i%} ! for adverbial form

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;

ProperNouns ;

VerbRoot ;

AdjRoot ;

Pronouns ;

Conjunctions ;

Determiners ;

Interjections ;

Adverbs ;

Digits ;

Punctuation ;


LEXICON NounTag
%<n%>: NounPl ;

LEXICON NPTop
%<pn%>%<top%>: NounInfl ;
%<pn%>%<top%>%+人:%>人 NounInfl ; ! person (effectively, citizen) じん
%<pn%>%<top%>%+軍:%>軍 NounInfl ; ! military ぐん
%<pn%>%<top%>%+国内:%>国内 NounInfl ; ! domestic こくない

LEXICON NounPl
%<sg%>: NounInfl ;
%<pl%>:%>たち NounInfl ;

LEXICON iAdj
%<adj%>%<npst%>:%>い #;
%<adj%>%+です%<cop%>%<pol%>%<npst%>:%>いです FinPart ;
%<adj%>%<ifml%>%<past%>:%>かった FinPart ;
%<adj%>%<ifml%>%<npst%>%<neg%>:%>くない FinPart ;
%<adj%>%<ifml%>%<past%>%<neg%>:%>くなかった FinPart ;
%<adj%>%<az%>:%>く # ;


LEXICON naAdjTag
%<adj%>: NounInfl ;
%<adj%>%<attr%>:%>な # ;

LEXICON NounInfl
%<cop%>%<pol%>%<npst%>:%>です FinPart ;
%<cop%>%<ifml%>%<npst%>:%>だ FinPart ;
%<pol%>%<cop%>%<past%>:%>でした FinPart ;
%<ifml%>%<cop%>%<past%>:%>だった FinPart ;
PostPos ;

LEXICON PostPos
%<tpc%>:%>は # ;
%<nom%>:%>が # ;
%<gen%>:%>の # ;
%<acc%>:%>を # ;
%<dat%>:%>に # ;
%<quot%>:%>と # ;
%<abl%>:%>から # ;
%<ins%>:%>で # ;
%<all%>:%>へ # ;
%<term%>:%>まで # ;
! mo
! ya
%<abs%>: # ;

LEXICON FinPart
%+わ%<mod%>:%>わ # ; !
%+よ%<mod%>:%>よ # ; ! Informing the hearer
%+ね%<mod%>:%>ね # ; ! Emphasis
%+か%<mod%>:%>か # ; ! Question

LEXICON UVerbInflCP
%<v%>: UVerbInfl ;
%<v%>%<caus%>:%>%{a%}せ RuVerbInfl ;
%<v%>%<pass%>:%>%{a%}れ RuVerbInfl ;
%<v%>%<caus%>%<pass%>:%>%{a%}せられ RuVerbInfl ;

LEXICON RuVerbInflCP
%<v%>: RuVerbInfl ;
%<v%>%<caus%>:%>させ RuVerbInfl ;
%<v%>%<pass%>:%>られ RuVerbInfl ;
%<v%>%<caus%>%<pass%>:%>させられ RuVerbInfl ;

LEXICON UVerbInfl
%<ifml%>%<npst%>: PostShortForm ;
%<ifml%>%<past%>:%>%{っ%}%{た%} PostShortForm ;
%<ifml%>%<npst%>%<neg%>:%>%{a%}ない PostShortForm ;
%<ifml%>%<past%>%<neg%>:%>%{a%}なかった PostShortForm ;
%<pol%>%<npst%>:%>%{i%}ます # ;
%<pol%>%<past%>:%>%{i%}ました # ;
%<pol%>%<npst%>%<neg%>:%>%{i%}ません # ;
%<pol%>%<past%>%<neg%>:%>%{i%}ませんでした # ;
%<cvb_te%>:%>%{っ%}%{て%} # ; ! needs to go to a post-te lexicon

LEXICON RuVerbInfl
%<ifml%>%<npst%>:%>る PostShortForm ;
%<ifml%>%<past%>:%>た PostShortForm ;
%<ifml%>%<npst%>%<neg%>:%>ない PostShortForm ;
%<ifml%>%<past%>%<neg%>:%>なかった PostShortForm ;
%<pol%>%<npst%>:%>ます # ;
%<pol%>%<past%>:%>ました # ;
%<pol%>%<npst%>%<neg%>:%>ません # ;
%<pol%>%<past%>%<neg%>:%>ませんでした # ;
%<cvb_te%>:%>て PostTeForm ;

LEXICON PostTeForm
# ;
%<prog%>:%>いる # ; ! this needs to get inflected

LEXICON PostShortForm
# ;
%<nz%>:%>こと NounInfl ;

LEXICON LOOP
%,:%, DIGITLEX ;
%.:%. DIGITLEX ;
%,:%, POST-DIGITS ;
%.:%. POST-DIGITS ;
      DIGITLEX ;
      POST-DIGITS ;

LEXICON DIGITLEX
%0:%0 LOOP ;
1:1   LOOP ;
2:2   LOOP ;
3:3   LOOP ;
4:4   LOOP ;
5:5   LOOP ;
6:6   LOOP ;
7:7   LOOP ;
8:8   LOOP ;
9:9   LOOP ;

LEXICON POST-DIGITS
%<num%>: PostPos ;
年%<num%>:年 PostPos ; ! year ねん
月%<num%>:月 PostPos ; ! month がつ
日%<num%>:日 PostPos ; ! day にち
時%<num%>:時 PostPos ; ! hour じ
分%<num%>:分 PostPos ; ! minute ぶん



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot
家:家 NounTag ; ! うち house
海外:かいがい NounTag ; ! かいがい foreign/overseas
会社:会社 NounTag ; ! かいしゃ company kaisha
可能:可能 NounTag ; ! かのう potential
外部:外部 NounTag ; ! がいぶ exterior
学校:学校 NounTag ; ! がっこう school gakkou
北側:北側 NounTag ; ! きたがわ north side kitagawa
基本:基本 NounTag ; ! きほん foundation
牛乳:牛乳 NounTag ; ! ぎゅうにゅう milk
区域:区域 NounTag ; ! くいき limits
空中:空中 NounTag ; ! くうちゅう air
グレープフルーツ:グレープフルーツ NounTag ; ! ぐれーぷふるーつ grapefruit
原爆:原爆 NounTag ; ! げんばく atomic
原爆ドーム:原爆ドーム NounTag ; ! げんばくどーむ atom bomb dome
原爆爆弾:原爆爆弾 NounTag ; ! げんばくばくだん atomic bomb
公園:公園 NounTag ; ! こうえん park
公式サイト:公式サイト NounTag ; ! こうしきさいと official site koushikisaito
構想:構想 NounTag ; ! こうそう plan
搾菜:搾菜 NounTag ; ! ざーさい Szechuan pickles
絞り:絞り NounTag ; ! しぼり
食用:食用 NounTag ; ! しょくよう edible
資料館:資料館 NounTag ; ! しりょうかん museum/archive
字:字 NounTag ; ! じ character
ジャム:ジャム NounTag ; ! じゃむ jam
ジュース:ジュース NounTag ; ! じゅーす juice
隙間:隙間 NounTag ; ! すきま gap
制限:制限 NounTag ; ! せいげん limit
世界:世界 NounTag ; ! せかい world
設計:設計 NounTag ; ! せっけい design/layout
選択:洗濯 NounTag ; ! せんたく laundry
タオル:タオル NounTag ; ! たおる towel
たまご:たまご NounTag ; ! たまご Dir/LR
卵:卵 NounTag ; ! たまご egg
地区:地区 NounTag ; ! ちく district
中止:中止 NounTag ; ! ちゅうし suspension/interruption
漬物:漬物 NounTag ; ! つけもの pickled vegetables
土:土 NounTag ; ! つち soil
動作:動作 NounTag ; ! どうさ action
同時:同時 NounTag ; ! どうじ simultaneous
ドライクリーニング:ドライクリーニング NounTag ; ! どらいくりーにんぐ dry cleaning
内部:内部 NounTag ; ! ないぶ interior
日本語:日本語 NounTag ; ! にほんご Japanese language nihongo
ニュアンス:ニュアンス NounTag ; ! にゅあんす nuance
繁華街:繁華街 NounTag ; ! はんかがい downtown
場合:場合 NounTag ; ! ばあい situation/case
媒介:媒介 NounTag ; ! ばいかい　intermediary
爆心地:爆心地 NounTag ; ! ばくしんち hypocentre
バラック:バラック NounTag ; ! ばらっく barracks
表現:表現 NounTag ; ! ひょうげん expression
広島:広島 NounTag ; ! ひろしま Hiroshima
広島市:広島市 NounTag ; ! ひろしまし Hiroshima city
広島市中央公園:広島市中央公園 NounTag ; ! ひろしまちゅうおこうえん Hiroshima central park
フラノクマリン:フラノクマリン NounTag ; ! ふらのくまりん furanocoumarin
物品:物品 NounTag ; ! ぶっぴん goods
平和:平和 NounTag ; ! へいわ peace
方法:方法 NounTag ; ! ほうほう method
マーマレード:マーマレード NounTag ; ! まーまれーど marmalade
水:水 NounTag ; ! みず water
水気:水気 NounTag ; ! みずけ moisture
流体:流体 NounTag ; ! りゅうたい fluid


LEXICON ProperNouns
アメリカ:アメリカ NPTop ; ! あめりか America
日本:日本 NPTop ; ! にほん Japan

LEXICON AdjRoot
多い:多 iAdj ; ! おおい many
最終的:最終的 naAdjTag ; ! さいしゅうてき final
様々:様々 naAdjTag ; ! さまざま various
静か:静か naAdjTag ; ! しずか quiet
相対的:相対的 naAdjTag ; ! そうたいてき relative
必要:必要 naAdjTag ; ! ひつよう necessary

LEXICON VerbRoot
遊ぶ:遊ぶ UVerbInflCP ; ! あそぶ play (games)
余る:余る UVerbInflCP ; ! あまる remain
謝る:謝る UVerbInflCP ; ! あやまる apologize
歩く:歩く UVerbInflCP ; ! あるく walk
言う:言う UVerbInflCP ; ! いう say
行う:行う UVerbInflCP ; ! おこなう occur
怒る:怒る UVerbInflCP ; ! おこる be angry
落とす:落とす UVerbInflCP ; ! おとす drop
踊る:踊る UVerbInflCP ; ! おどる dance
思う:思う UVerbInflCP ; ! おもう think
買う:買う UVerbInflCP ; ! かう buy
書く:書く UVerbInflCP ; ! かく write
かむ:かむ UVerbInflCP ; ! かむ bite
聞く:聞く UVerbInflCP ; ! きく listen
殺す:殺す UVerbInflCP ; ! ころす kill
死ぬ:死ぬ UVerbInflCP ; ! しぬ die
立つ:立つ UVerbInflCP ; ! たつ stand
食べる:食べ RuVerbInflCP ; ! たべる eat
使う:使う UVerbInflCP ; ! つかう use
作る:作る UVerbInflCP ; ! つくる make
残る:残る UVerbInflCP ; ! のこる remain
入る:入る UVerbInflCP ; ! はいる enter
運ぶ:運ぶ UVerbInflCP ; ! はこぶ carry
話す:話す UVerbInflCP ; ! はなす talk
含まる:含まる UVerbInflCP ; ! ふくまれる include
向ける:向け UVerbInflCP ; ! むける turn
持つ:持つ UVerbInflCP ; ! もつ carry
呼ぶ:呼ぶ UVerbInflCP ; ! よぶ call (something a name/yell out)
笑う:笑う UVerbInflCP ; ! わらう laugh
する%<v%>%<ifml%>%<npst%>:する PostShortForm ; ! do
する%<v%>%<pol%>%<npst%>:します # ;
する%<v%>%<cvb_te%>:して # ;
する%<v%>%<caus%>:させ RuVerbInfl ;
する%<v%>%<pass%>:され RuVerbInfl ;

LEXICON Pronouns
私%<prn%>%<pers%>%<p1%>:私 NounPl ; ! I
おまえ%<prn%>%<pers%>%<p2%>:おまえ NounPl ;

LEXICON Determiners
この%<det%>%<dem%>:この # ; ! this
その%<det%>%<dem%>:その # ; ! that
あの%<det%>%<dem%>:あの # ; ! that thing over there

LEXICON Conjunctions
しかし%<cnjcoo%>:しかし # ; ! however
そして%<cnjcoo%>:そして # ; ! additionally
また%<cnjcoo%>:また # ; ! also

LEXICON Adverbs
特に%<adv%>:特に # ; ! とくに especially
もう%<adv%>:もう # ; ! もう already

LEXICON Interjections
ありがとう%<ij%>:ありがとう # ; ! thanks
ありがとうございます%<ij%>:ありがとうございます # ; ! thank you
こんにちは%<ij%>:こんにちは # ; ! greetings
はい%<ij%>:はい # ; ! yes

LEXICON Digits
DIGITLEX ;

LEXICON Punctuation
。%<sent%>:。 # ;
、%<cm%>:、 # ;
「%<lquot%>:「 # ;
」%<rquot%>:」 # ;
（%<lpar%>:（ # ;
）%<rpar%>:） # ;


! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst
